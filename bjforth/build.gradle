// -*- mode: groovy; groovy-indent-offset: 2; -*-
import org.gradle.api.tasks.JavaExec
import org.gradle.api.tasks.compile.JavaCompile
import org.gradle.api.tasks.testing.Test

plugins {
  id 'application'
  id 'com.diffplug.spotless' version '6.25.0'
  id 'jacoco'
}

repositories {
  mavenCentral()
}

application {
  mainClass = 'bjforth.BJForth'
}

sourceSets {
  testLib {
    java.srcDir file('src/test-lib/java')
    resources.srcDir file('src/test-lib/resources')
    compileClasspath += sourceSets.main.output + sourceSets.main.compileClasspath
    runtimeClasspath += output + compileClasspath
  }
}

configurations {
  testLibImplementation.extendsFrom implementation
  testLibApi.extendsFrom api
  testLib.extendsFrom implementation
}

dependencies {
  testLib sourceSets.testLib.output
  testLibImplementation 'org.apache.commons:commons-lang3:3.17.0'
  testLibImplementation 'org.assertj:assertj-core:3.26.3'

  testImplementation sourceSets.testLib.runtimeClasspath

  testImplementation 'org.junit.jupiter:junit-jupiter:5.11.3'
  testImplementation 'org.junit.jupiter:junit-jupiter-params:5.11.3'
  testImplementation 'org.assertj:assertj-core:3.26.3'

  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
}

spotless {
  encoding 'UTF-8'
  lineEndings 'UNIX'
  format 'misc', {
    target '*.gradle', '.gitignore'
    trimTrailingWhitespace()
    indentWithSpaces()
    endWithNewline()
  }
  java {
    endWithNewline()
    importOrder()
    removeUnusedImports()
    googleJavaFormat()
    licenseHeader '''/*
 * Copyright $YEAR Bahman Movaqar
 *
 * This file is part of bjForth.
 *
 * bjForth is free software: you can redistribute it and/or modify it
 * under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * bjForth is distributed in the hope that it will be useful, but WITHOUT
 * ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
 * FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License
 * for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with bjForth. If not, see <https://www.gnu.org/licenses/>.
 */
'''
  }
}

tasks.withType(Test).configureEach {
  useJUnitPlatform()
}

tasks.withType(JavaCompile).configureEach {
  options.encoding = 'UTF-8'
  options.incremental = true
  options.release.set(21)
  options.compilerArgs << '-Xlint:deprecation' << '-Xlint:unchecked' << '-parameters' << '--enable-preview'
  dependsOn 'spotlessApply'
}

tasks.withType(JavaExec).configureEach {
}

jacoco {
  toolVersion = '0.8.12'
}

test {
  finalizedBy jacocoTestReport
}

jacocoTestReport {
  dependsOn test
  reports {
    xml.required = true
    csv.required = true
    html.required = false
  }
}
